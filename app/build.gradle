import mobile.buildsrc.Libs

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    androidExtensions {
        experimental = true
    }

    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-Debug'
            minifyEnabled false

            //Gradle Speed Hacks
            crunchPngs false
            manifestPlaceholders = [enableFirebaseMessaging: "true",
                                    enableFirebaseAnalytics: "false",
                                    enableCrashReporting: "false"]
            splits.abi.enable = false
            splits.density.enable = false
        }

        release {
            minifyEnabled false
            manifestPlaceholders = [enableFirebaseMessaging: "true",
                                    enableFirebaseAnalytics: "true",
                                    enableCrashReporting: "true"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {

        /**
         * Google Play Store Build
         */
        production {
            dimension "default"
            applicationId "com.himanshu.tasked"
            resValue "string", "app_name", "Tasked"
        }

        /**
         * development Build
         */
        development {
            dimension "default"
            applicationId "com.himanshu.tasked.development"
            resValue "string", "app_name", "Tasked Dev"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(':androidbase')
    implementation project(':feature_auth')
    implementation project(':cache')
    implementation project(':data')
    implementation project(':remote')

    testImplementation Libs.junit
    testImplementation Libs.mockitoInline
    testImplementation Libs.mockitoKotlin
    testImplementation Libs.AndroidX.archCoreTesting

    androidTestImplementation Libs.AndroidX.Test.core
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.espressoCore

    debugImplementation Libs.leakCanary2
    debugImplementation Libs.Facebook.stetho

    implementation Libs.Google.playRefer
    implementation Libs.Google.playCore
    implementation Libs.Google.smsRetriver

    implementation Libs.circularImageView
    implementation Libs.AirBnb.lottie
    implementation Libs.zoomableImageView
    implementation Libs.appIntro

    implementation Libs.cameraIntegrator
    implementation Libs.Kotlin.stdlib
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.Lifecycle.extensions
    implementation Libs.AndroidX.multiDex

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.vectorDrawables
    implementation Libs.AndroidX.constraintlayout

    implementation Libs.AndroidX.Navigation.navigationUi
    implementation Libs.AndroidX.Navigation.navigationUiKtx
    implementation Libs.AndroidX.Navigation.navigationFragment
    implementation Libs.AndroidX.Navigation.navigationFragmentKtx

    implementation Libs.Coroutines.core
    implementation Libs.Coroutines.rx2
    implementation Libs.Coroutines.android
    implementation Libs.AndroidX.Lifecycle.lifeCyleViewModel
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    implementation 'androidx.fragment:fragment-ktx:1.2.0'
    implementation "androidx.activity:activity-ktx:1.1.0"

    implementation Libs.Google.material
    implementation Libs.Google.auth

    implementation Libs.Firebase.firebaseCore
    implementation Libs.Firebase.firebaseAnalytics
    implementation Libs.Firebase.crashlytics
    implementation Libs.Firebase.inAppMessaging
    implementation Libs.Firebase.firebaseMessaging
    implementation Libs.Firebase.dynamicLinks

    kapt Libs.Dagger.androidProcessor
    kapt Libs.Dagger.compiler
    implementation Libs.Dagger.androidSupport
    implementation Libs.Dagger.dagger

    //These two are here just to make dagger 2 happy
    implementation Libs.AndroidX.Room.runtime
    implementation Libs.Square.OkHttp.loggingInterceptor
}
